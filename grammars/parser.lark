%import common.INT
%import common.WS_INLINE

%ignore WS_INLINE

?start: lhs "=>" rhs						-> add_rule
	| "=" var*								-> set_facts
	| "?" var*								-> query_vars
	| "@info"								-> show_info
	| "@del" index							-> del_rule
	| "@verbose" "on"						-> verbose_on
	| "@verbose" "off"						-> verbose_off
	| "@vis"								-> visualize
	| "@reset"								-> reset
	| "@dance"								-> dance
	| "@doge"								-> doge

?lhs: alpha

?alpha: bravo
	| alpha "^" bravo						-> xor_op

?bravo: charlie
	| bravo "|" charlie						-> or_op

?charlie: delta
	| charlie "+" delta						-> and_op

?delta: echo
	| "!" echo								-> not_op

?echo: var
	| "(" alpha ")"							-> parentheses

?rhs: var									-> rhs_var
	| rhs "+" var							-> rhs_and_var

?var: ("A".."Z")							-> parse_var

?index : INT								-> parse_index

